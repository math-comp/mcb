<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <meta http-equiv="content-type" content="text/html;charset=utf-8" />

    <link rel="stylesheet" href="../external/bootstrap.min.css" />

    <title>Use Coq in Your Browser: The Js Coq Theorem Prover Online IDE!</title>
  </head>

<body>

  <div id="ide-wrapper" class="toggled">
  <div id="code-wrapper">
  <div id="document">

<h1>Cpdt.StackMachine</h1>
<div style='background: #FF4444; border: 2px black; padding: 12px;'>
  <h2><strong>Warning!!!!</strong></h2>

  <p>
    This file is used as a demonstration of the jsCoq tool and is not
    an official version of the <a href="http://adam.chlipala.net/cpdt/">Certified Programming with Depedent Types</a> book by Adam Chlipala. According to the license, the file is copied verbatim.
</p>
<pre>
Copyright (c) 2008-2012, 2015, Adam Chlipala
 *
 * This work is licensed under a
 * Creative Commons Attribution-Noncommercial-No Derivative Works 3.0
 * Unported License.
 * The license text is available at:
 *   http://creativecommons.org/licenses/by-nc-nd/3.0/
 *)
</pre>
</div>

<div><p>
 <p>
 I will start off by jumping right in to a fully worked set of examples, building certified compilers from increasingly complicated source languages to stack machines.  We will meet a few useful tactics and see how they can be used in manual proofs, and we will also see how easily these proofs can be automated instead.  This chapter is not meant to give full explanations of the features that are employed.  Rather, it is meant more as an advertisement of what is possible.  Later chapters will introduce all of the concepts in bottom-up fashion.  In other words, it is expected that most readers will not understand what exactly is going on here, but I hope this demo will whet your appetite for the remaining chapters!
<p>
As always, you can step through the source file <tt>StackMachine.v</tt> for this chapter interactively in Proof General.  Alternatively, to get a feel for the whole lifecycle of creating a Coq development, you can enter the pieces of source code in this chapter in a new <tt>.v</tt> file in an Emacs buffer.  If you do the latter, include these three lines at the start of the file. </div>
<div><textarea id='coq-ta-2'>
From Coq  Require Import Bool.Bool Lists.List Arith.Arith.
From Cpdt Require Import CpdtTactics.

Set Implicit Arguments.
Set Asymmetric Patterns.
</textarea></div>
<div><p>
In general, similar commands will be hidden in the book rendering of each chapter's source code, so you will need to insert them in from-scratch replayings of the code that is presented.  To be more specific, every chapter begins with the above three lines, with the import list tweaked as appropriate, considering which definitions the chapter uses.  The second command above affects the default behavior of definitions regarding type inference, and the third allows for more concise pattern-matching syntax in Coq versions 8.5 and higher (having no effect in earlier versions). <p>
<h1>
 Arithmetic Expressions Over Natural Numbers
</h1>
<p>
 We will begin with that staple of compiler textbooks, arithmetic expressions over a single type of numbers. <p>
<h2>
 Source Language
</h2>
<p>
 We begin with the syntax of the source language. </div>
<div><textarea id='coq-ta-3'>
Inductive binop : Set := Plus | Times.
</textarea></div>
<div><p>
Our first line of Coq code should be unsurprising to ML and Haskell programmers.  We define an algebraic datatype <tt>binop</tt> to stand for the binary operators of our source language.  There are just two wrinkles compared to ML and Haskell.  First, we use the keyword <tt>Inductive</tt>, in place of <tt>data</tt>, <tt>datatype</tt>, or <tt>type</tt>.  This is not just a trivial surface syntax difference; inductive types in Coq are much more expressive than garden variety algebraic datatypes, essentially enabling us to encode all of mathematics, though we begin humbly in this chapter.  Second, there is the <tt>:</tt> <tt>Set</tt> fragment, which declares that we are defining a datatype that should be thought of as a constituent of programs.  Later, we will see other options for defining datatypes in the universe of proofs or in an infinite hierarchy of universes, encompassing both programs and proofs, that is useful in higher-order constructions. </div>
<div><textarea id='coq-ta-4'>
Inductive exp : Set :=
| Const : nat -> exp
| Binop : binop -> exp -> exp -> exp.
</textarea></div>
<div><p>
Now we define the type of arithmetic expressions.  We write that a constant may be built from one argument, a natural number; and a binary operation may be built from a choice of operator and two operand expressions.
<p>
A note for readers following along in the PDF version: coqdoc supports pretty-printing of tokens in LaTeX or HTML.  Where you see a right arrow character, the source contains the ASCII text <tt>-</tt>>.  Other examples of this substitution appearing in this chapter are a double right arrow for <tt>=</tt>>, the inverted 'A' symbol for <tt>forall</tt>, and the Cartesian product 'X' for <tt>*</tt>.  When in doubt about the ASCII version of a symbol, you can consult the chapter source code.
<p>
Now we are ready to say what programs in our expression language mean.  We will do this by writing an interpreter that can be thought of as a trivial operational or denotational semantics.  (If you are not familiar with these semantic techniques, no need to worry: we will stick to <quote>common sense</quote> constructions.) </div>
<div><textarea id='coq-ta-5'>
Definition binopDenote (b : binop) : nat -> nat -> nat :=
  match b with
    | Plus => plus
    | Times => mult
  end.
</textarea></div>
<div><p>
The meaning of a binary operator is a binary function over naturals, defined with pattern-matching notation analogous to the <tt>case</tt> and <tt>match</tt> of ML and Haskell, and referring to the functions <tt>plus</tt> and <tt>mult</tt> from the Coq standard library.  The keyword <tt>Definition</tt> is Coq's all-purpose notation for binding a term of the programming language to a name, with some associated syntactic sugar, like the notation we see here for defining a function.  That sugar could be expanded to yield this definition:
<pre class="inline-coq" data-lang="coq">
Definition binopDenote : binop -> nat -> nat -> nat := fun (b : binop) =>
  match b with
    | Plus => plus
    | Times => mult
  end.<p>
</pre><p>
In this example, we could also omit all of the type annotations, arriving at:
<pre class="inline-coq" data-lang="coq">
Definition binopDenote := fun b =>
  match b with
    | Plus => plus
    | Times => mult
  end.<p>
</pre><p>
Languages like Haskell and ML have a convenient <em>principal types</em> property, which gives us strong guarantees about how effective type inference will be.  Unfortunately, Coq's type system is so expressive that any kind of <quote>complete</quote> type inference is impossible, and the task even seems to be hard in practice.  Nonetheless, Coq includes some very helpful heuristics, many of them copying the workings of Haskell and ML type-checkers for programs that fall in simple fragments of Coq's language.
<p>
This is as good a time as any to mention the profusion of different languages associated with Coq.  The theoretical foundation of Coq is a formal system called the <em>Calculus of Inductive Constructions</em> (CIC), which is an extension of the older <em>Calculus of Constructions</em> (CoC).  CIC is quite a spartan foundation, which is helpful for proving metatheory but not so helpful for real development.  Still, it is nice to know that it has been proved that CIC enjoys properties like <em>strong normalization</em> , meaning that every program (and, more importantly, every proof term) terminates; and <em>relative consistency</em>  with systems like versions of Zermelo-Fraenkel set theory, which roughly means that you can believe that Coq proofs mean that the corresponding propositions are <quote>really true,</quote> if you believe in set theory.
<p>
Coq is actually based on an extension of CIC called Gallina.  The text after the <tt>:=</tt> and before the period in the last code example is a term of Gallina.  Gallina includes several useful features that must be considered as extensions to CIC.  The important metatheorems about CIC have not been extended to the full breadth of the features that go beyond the formalized language, but most Coq users do not seem to lose much sleep over this omission.
<p>
Next, there is Ltac, Coq's domain-specific language for writing proofs and decision procedures. We will see some basic examples of Ltac later in this chapter, and much of this book is devoted to more involved Ltac examples.
<p>
Finally, commands like <tt>Inductive</tt> and <tt>Definition</tt> are part of the Vernacular, which includes all sorts of useful queries and requests to the Coq system.  Every Coq source file is a series of vernacular commands, where many command forms take arguments that are Gallina or Ltac programs.  (Actually, Coq source files are more like <em>trees</em> of vernacular commands, thanks to various nested scoping constructs.)
<p>

<p>
We can give a simple definition of the meaning of an expression: </div>
<div><textarea id='coq-ta-6'>
Fixpoint expDenote (e : exp) : nat :=
  match e with
    | Const n => n
    | Binop b e1 e2 => (binopDenote b) (expDenote e1) (expDenote e2)
  end.

</textarea></div>
<div><p>
We declare explicitly that this is a recursive definition, using the keyword <tt>Fixpoint</tt>.  The rest should be old hat for functional programmers. <p>
 It is convenient to be able to test definitions before starting to prove things about them.  We can verify that our semantics is sensible by evaluating some sample uses, using the command <tt>Eval</tt>.  This command takes an argument expressing a <em>reduction strategy</em>, or an <quote>order of evaluation.</quote>  Unlike with ML, which hardcodes an <em>eager</em> reduction strategy, or Haskell, which hardcodes a <em>lazy</em> strategy, in Coq we are free to choose between these and many other orders of evaluation, because all Coq programs terminate.  In fact, Coq silently checked termination of our <tt>Fixpoint</tt> definition above, using a simple heuristic based on monotonically decreasing size of arguments across recursive calls.  Specifically, recursive calls must be made on arguments that were pulled out of the original recursive argument with <tt>match</tt> expressions.  (In Chapter 7, we will see some ways of getting around this restriction, though simply removing the restriction would leave Coq useless as a theorem proving tool, for reasons we will start to learn about in the next chapter.)
<p>
To return to our test evaluations, we run the <tt>Eval</tt> command using the <tt>simpl</tt> evaluation strategy, whose definition is best postponed until we have learned more about Coq's foundations, but which usually gets the job done. </div>
<div><textarea id='coq-ta-7'>
Eval simpl in expDenote (Const 42).
</textarea></div>
<div><p>
<tt>=</tt> <tt>42</tt> <tt>:</tt> <tt>nat</tt> </div>
<div><textarea id='coq-ta-8'>
Eval simpl in expDenote (Binop Plus (Const 2) (Const 2)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>4</tt> <tt>:</tt> <tt>nat</tt> </div>
<div><textarea id='coq-ta-9'>
Eval simpl in expDenote (Binop Times (Binop Plus (Const 2) (Const 2)) (Const 7)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>28</tt> <tt>:</tt> <tt>nat</tt> <p>
 Nothing too surprising goes on here, so we are ready to move on to the target language of our compiler. <p>
<h2>
 Target Language
</h2>
<p>
 We will compile our source programs onto a simple stack machine, whose syntax is: </div>
<div><textarea id='coq-ta-10'>
Inductive instr : Set :=
| iConst : nat -> instr
| iBinop : binop -> instr.

Definition prog := list instr.
Definition stack := list nat.
</textarea></div>
<div><p>
An instruction either pushes a constant onto the stack or pops two arguments, applies a binary operator to them, and pushes the result onto the stack.  A program is a list of instructions, and a stack is a list of natural numbers.
<p>
We can give instructions meanings as functions from stacks to optional stacks, where running an instruction results in <tt>None</tt> in case of a stack underflow and results in <tt>Some</tt> <tt>s'</tt> when the result of execution is the new stack <tt>s'</tt>.  The infix operator <tt>::</tt> is <quote>list cons</quote> from the Coq standard library. </div>
<div><textarea id='coq-ta-11'>
Definition instrDenote (i : instr) (s : stack) : option stack :=
  match i with
    | iConst n => Some (n :: s)
    | iBinop b =>
      match s with
        | arg1 :: arg2 :: s' => Some ((binopDenote b) arg1 arg2 :: s')
        | _ => None
      end
  end.

</textarea></div>
<div><p>
With <tt>instrDenote</tt> defined, it is easy to define a function <tt>progDenote</tt>, which iterates application of <tt>instrDenote</tt> through a whole program. </div>
<div><textarea id='coq-ta-12'>
Fixpoint progDenote (p : prog) (s : stack) : option stack :=
  match p with
    | nil => Some s
    | i :: p' =>
      match instrDenote i s with
        | None => None
        | Some s' => progDenote p' s'
      end
  end.
</textarea></div>
<div><p>
With the two programming languages defined, we can turn to the compiler definition. <p>
<h2>
 Translation
</h2>
<p>
 Our compiler itself is now unsurprising.  The list concatenation operator <tt>++</tt> comes from the Coq standard library. </div>
<div><textarea id='coq-ta-13'>
Fixpoint compile (e : exp) : prog :=
  match e with
    | Const n => iConst n :: nil
    | Binop b e1 e2 => compile e2 ++ compile e1 ++ iBinop b :: nil
  end.
</textarea></div>
<div><p>
Before we set about proving that this compiler is correct, we can try a few test runs, using our sample programs from earlier. </div>
<div><textarea id='coq-ta-14'>
Eval simpl in compile (Const 42).
</textarea></div>
<div><p>
<tt>=</tt> <tt>iConst</tt> <tt>42</tt> <tt>::</tt> <tt>nil</tt> <tt>:</tt> <tt>prog</tt> </div>
<div><textarea id='coq-ta-15'>
Eval simpl in compile (Binop Plus (Const 2) (Const 2)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>iConst</tt> <tt>2</tt> <tt>::</tt> <tt>iConst</tt> <tt>2</tt> <tt>::</tt> <tt>iBinop</tt> <tt>Plus</tt> <tt>::</tt> <tt>nil</tt> <tt>:</tt> <tt>prog</tt> </div>
<div><textarea id='coq-ta-16'>
Eval simpl in compile (Binop Times (Binop Plus (Const 2) (Const 2)) (Const 7)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>iConst</tt> <tt>7</tt> <tt>::</tt> <tt>iConst</tt> <tt>2</tt> <tt>::</tt> <tt>iConst</tt> <tt>2</tt> <tt>::</tt> <tt>iBinop</tt> <tt>Plus</tt> <tt>::</tt> <tt>iBinop</tt> <tt>Times</tt> <tt>::</tt> <tt>nil</tt> <tt>:</tt> <tt>prog</tt> <p>
 We can also run our compiled programs and check that they give the right results. </div>
<div><textarea id='coq-ta-17'>
Eval simpl in progDenote (compile (Const 42)) nil.
</textarea></div>
<div><p>
<tt>=</tt> <tt>Some</tt> <tt>(42</tt> <tt>::</tt> <tt>nil)</tt> <tt>:</tt> <tt>option</tt> <tt>stack</tt> </div>
<div><textarea id='coq-ta-18'>
Eval simpl in progDenote (compile (Binop Plus (Const 2) (Const 2))) nil.
</textarea></div>
<div><p>
<tt>=</tt> <tt>Some</tt> <tt>(4</tt> <tt>::</tt> <tt>nil)</tt> <tt>:</tt> <tt>option</tt> <tt>stack</tt> </div>
<div><textarea id='coq-ta-19'>
Eval simpl in progDenote (compile (Binop Times (Binop Plus (Const 2) (Const 2))
  (Const 7))) nil.
</textarea></div>
<div><p>
<tt>=</tt> <tt>Some</tt> <tt>(28</tt> <tt>::</tt> <tt>nil)</tt> <tt>:</tt> <tt>option</tt> <tt>stack</tt> <p>
 So far so good, but how can we be sure the compiler operates correctly for <em>all</em> input programs? <p>
<h2>
 Translation Correctness
</h2>
<p>
 We are ready to prove that our compiler is implemented correctly.  We can use a new vernacular command <tt>Theorem</tt> to start a correctness proof, in terms of the semantics we defined earlier: </div>
<div><textarea id='coq-ta-20'>
Theorem compile_correct : forall e, progDenote (compile e) nil = Some (expDenote e :: nil).
</textarea></div>
<div><p>
Though a pencil-and-paper proof might clock out at this point, writing <quote>by a routine induction on <tt>e</tt>,</quote> it turns out not to make sense to attack this proof directly.  We need to use the standard trick of <em>strengthening the induction hypothesis</em>.  We do that by proving an auxiliary lemma, using the command <tt>Lemma</tt> that is a synonym for <tt>Theorem</tt>, conventionally used for less important theorems that appear in the proofs of primary theorems. </div>
<div><textarea id='coq-ta-21'>
Abort.

Lemma compile_correct' : forall e p s,
  progDenote (compile e ++ p) s = progDenote p (expDenote e :: s).
</textarea></div>
<div><p>
After the period in the <tt>Lemma</tt> command, we are in <em>the interactive proof-editing mode</em>.  We find ourselves staring at this ominous screen of text:
<p>
<pre class="inline-coq" data-lang="coq">
1 subgoal
  
 ============================
  forall (e : exp) (p : list instr) (s : stack),
   progDenote (compile e ++ p) s = progDenote p (expDenote e :: s)  
 <p>
</pre><p>
Coq seems to be restating the lemma for us.  What we are seeing is a limited case of a more general protocol for describing where we are in a proof.  We are told that we have a single subgoal.  In general, during a proof, we can have many pending subgoals, each of which is a logical proposition to prove.  Subgoals can be proved in any order, but it usually works best to prove them in the order that Coq chooses.
<p>
Next in the output, we see our single subgoal described in full detail.  There is a double-dashed line, above which would be our free variables and hypotheses, if we had any.  Below the line is the conclusion, which, in general, is to be proved from the hypotheses.
<p>
We manipulate the proof state by running commands called <em>tactics</em>.  Let us start out by running one of the most important tactics:
</div>
<div><textarea id='coq-ta-22'>
  induction e.
</textarea></div>
<div><p>
We declare that this proof will proceed by induction on the structure of the expression <tt>e</tt>.  This swaps out our initial subgoal for two new subgoals, one for each case of the inductive proof:
<p>
<pre class="inline-coq" data-lang="coq">
2 subgoals

 n : nat
 ============================
 forall (s : stack) (p : list instr),
   progDenote (compile (Const n) ++ p) s =
   progDenote p (expDenote (Const n) :: s)

subgoal 2 is

  forall (s : stack) (p : list instr),
    progDenote (compile (Binop b e1 e2) ++ p) s =
    progDenote p (expDenote (Binop b e1 e2) :: s)
 <p>
</pre><p>
The first and current subgoal is displayed with the double-dashed line below free variables and hypotheses, while later subgoals are only summarized with their conclusions.  We see an example of a free variable in the first subgoal; <tt>n</tt> is a free variable of type <tt>nat</tt>.  The conclusion is the original theorem statement where <tt>e</tt> has been replaced by <tt>Const</tt> <tt>n</tt>.  In a similar manner, the second case has <tt>e</tt> replaced by a generalized invocation of the <tt>Binop</tt> expression constructor.  We can see that proving both cases corresponds to a standard proof by structural induction.
<p>
We begin the first case with another very common tactic.
</div>
<div><textarea id='coq-ta-23'>
  intros.
</textarea></div>
<div><p>
The current subgoal changes to:
<pre class="inline-coq" data-lang="coq">
 n : nat
 s : stack
 p : list instr
 ============================
 progDenote (compile (Const n) ++ p) s =
 progDenote p (expDenote (Const n) :: s)
 <p>
</pre><p>
We see that <tt>intros</tt> changes <tt>forall</tt>-bound variables at the beginning of a goal into free variables.
<p>
To progress further, we need to use the definitions of some of the functions appearing in the goal.  The <tt>unfold</tt> tactic replaces an identifier with its definition.
</div>
<div><textarea id='coq-ta-24'>
  unfold compile.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
 n : nat
 s : stack
 p : list instr
 ============================
 progDenote ((iConst n :: nil) ++ p) s =
 progDenote p (expDenote (Const n) :: s)
 <p>
</pre></div>
<div><textarea id='coq-ta-25'>
  unfold expDenote.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
 n : nat
 s : stack
 p : list instr
 ============================
 progDenote ((iConst n :: nil) ++ p) s = progDenote p (n :: s)
 <p>
</pre><p>
We only need to unfold the first occurrence of <tt>progDenote</tt> to prove the goal.  An <tt>at</tt> clause used with <tt>unfold</tt> specifies a particular occurrence of an identifier to unfold, where we count occurrences from left to right. </div>
<div><textarea id='coq-ta-26'>
  unfold progDenote at 1.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
 n : nat
 s : stack
 p : list instr
 ============================
  (fix progDenote (p0 : prog) (s0 : stack) {struct p0} : 
    option stack :=
      match p0 with
      | nil => Some s0
      | i :: p' =>
          match instrDenote i s0 with
          | Some s' => progDenote p' s'
          | None => None (A:=stack)
          end
      end) ((iConst n :: nil) ++ p) s =
   progDenote p (n :: s)
 <p>
</pre><p>
This last <tt>unfold</tt> has left us with an anonymous recursive definition of <tt>progDenote</tt> (similarly to how <tt>fun</tt> or <quote>lambda</quote> constructs in general allow anonymous non-recursive functions), which will generally happen when unfolding recursive definitions.  Note that Coq has automatically renamed the <tt>fix</tt> arguments <tt>p</tt> and <tt>s</tt> to <tt>p0</tt> and <tt>s0</tt>, to avoid clashes with our local free variables.  There is also a subterm <tt>None</tt> <tt>(A:=stack)</tt>, which has an annotation specifying that the type of the term ought to be <tt>option</tt> <tt>stack</tt>.  This is phrased as an explicit instantiation of a named type parameter <tt>A</tt> from the definition of <tt>option</tt>.
<p>
Fortunately, in this case, we can eliminate the complications of anonymous recursion right away, since the structure of the argument (<tt>iConst</tt> <tt>n</tt> <tt>::</tt> <tt>nil)</tt> <tt>++</tt> <tt>p</tt> is known, allowing us to simplify the internal pattern match with the <tt>simpl</tt> tactic, which applies the same reduction strategy that we used earlier with <tt>Eval</tt> (and whose details we still postpone).
</div>
<div><textarea id='coq-ta-27'>
  simpl.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
 n : nat
 s : stack
 p : list instr
 ============================
 (fix progDenote (p0 : prog) (s0 : stack) {struct p0} : 
  option stack :=
    match p0 with
    | nil => Some s0
    | i :: p' =>
        match instrDenote i s0 with
        | Some s' => progDenote p' s'
        | None => None (A:=stack)
        end
    end) p (n :: s) = progDenote p (n :: s)
 <p>
</pre><p>
Now we can unexpand the definition of <tt>progDenote</tt>:
</div>
<div><textarea id='coq-ta-28'>
  fold progDenote.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
 n : nat
 s : stack
 p : list instr
 ============================
 progDenote p (n :: s) = progDenote p (n :: s)
 <p>
</pre><p>
It looks like we are at the end of this case, since we have a trivial equality.  Indeed, a single tactic finishes us off:
</div>
<div><textarea id='coq-ta-29'>
  reflexivity.
</textarea></div>
<div><p>
On to the second inductive case:
<p>
<pre class="inline-coq" data-lang="coq">
  b : binop
  e1 : exp
  IHe1 : forall (s : stack) (p : list instr),
         progDenote (compile e1 ++ p) s = progDenote p (expDenote e1 :: s)
  e2 : exp
  IHe2 : forall (s : stack) (p : list instr),
         progDenote (compile e2 ++ p) s = progDenote p (expDenote e2 :: s)
  ============================
   forall (s : stack) (p : list instr),
   progDenote (compile (Binop b e1 e2) ++ p) s =
   progDenote p (expDenote (Binop b e1 e2) :: s)
 <p>
</pre><p>
We see our first example of hypotheses above the double-dashed line.  They are the inductive hypotheses <tt>IHe1</tt> and <tt>IHe2</tt> corresponding to the subterms <tt>e1</tt> and <tt>e2</tt>, respectively.
<p>
We start out the same way as before, introducing new free variables and unfolding and folding the appropriate definitions.  The seemingly frivolous <tt>unfold</tt>/<tt>fold</tt> pairs are actually accomplishing useful work, because <tt>unfold</tt> will sometimes perform easy simplifications.  </div>
<div><textarea id='coq-ta-30'>
  intros.
  unfold compile.
  fold compile.
  unfold expDenote.
  fold expDenote.
</textarea></div>
<div><p>
Now we arrive at a point where the tactics we have seen so far are insufficient.  No further definition unfoldings get us anywhere, so we will need to try something different.
<p>
<pre class="inline-coq" data-lang="coq">
  b : binop
  e1 : exp
  IHe1 : forall (s : stack) (p : list instr),
         progDenote (compile e1 ++ p) s = progDenote p (expDenote e1 :: s)
  e2 : exp
  IHe2 : forall (s : stack) (p : list instr),
         progDenote (compile e2 ++ p) s = progDenote p (expDenote e2 :: s)
  s : stack
  p : list instr
  ============================
   progDenote ((compile e2 ++ compile e1 ++ iBinop b :: nil) ++ p) s =
   progDenote p (binopDenote b (expDenote e1) (expDenote e2) :: s)
 <p>
</pre><p>
What we need is the associative law of list concatenation, which is available as a theorem <tt>app_assoc_reverse</tt> in the standard library.  (Here and elsewhere, it is possible to tell the difference between inputs and outputs to Coq by periods at the ends of the inputs.) </div>
<div><textarea id='coq-ta-31'>
Check app_assoc_reverse.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
app_assoc_reverse
     : forall (A : Type) (l m n : list A), (l ++ m) ++ n = l ++ m ++ n
 <p>
</pre><p>
If we did not already know the name of the theorem, we could use the <tt>SearchRewrite</tt> command to find it, based on a pattern that we would like to rewrite: </div>
<div><textarea id='coq-ta-32'>
SearchRewrite ((_ ++ _) ++ _).
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
app_assoc_reverse:
  forall (A : Type) (l m n : list A), (l ++ m) ++ n = l ++ m ++ n<p>
</pre>
<pre class="inline-coq" data-lang="coq">
app_assoc: forall (A : Type) (l m n : list A), l ++ m ++ n = (l ++ m) ++ n
 <p>
</pre><p>
We use <tt>app_assoc_reverse</tt> to perform a rewrite:  </div>
<div><textarea id='coq-ta-33'>
  rewrite app_assoc_reverse.
</textarea></div>
<div><p>
changing the conclusion to:
<p>
<pre class="inline-coq" data-lang="coq">
   progDenote (compile e2 ++ (compile e1 ++ iBinop b :: nil) ++ p) s =
   progDenote p (binopDenote b (expDenote e1) (expDenote e2) :: s)
 <p>
</pre><p>
Now we can notice that the lefthand side of the equality matches the lefthand side of the second inductive hypothesis, so we can rewrite with that hypothesis, too. </div>
<div><textarea id='coq-ta-34'>
  rewrite IHe2.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
   progDenote ((compile e1 ++ iBinop b :: nil) ++ p) (expDenote e2 :: s) =
   progDenote p (binopDenote b (expDenote e1) (expDenote e2) :: s)
 <p>
</pre><p>
The same process lets us apply the remaining hypothesis. </div>
<div><textarea id='coq-ta-35'>
  rewrite app_assoc_reverse.
  rewrite IHe1.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
   progDenote ((iBinop b :: nil) ++ p) (expDenote e1 :: expDenote e2 :: s) =
   progDenote p (binopDenote b (expDenote e1) (expDenote e2) :: s)
 <p>
</pre><p>
Now we can apply a similar sequence of tactics to the one that ended the proof of the first case.
</div>
<div><textarea id='coq-ta-36'>
  unfold progDenote at 1.
  simpl.
  fold progDenote.
  reflexivity.
</textarea></div>
<div><p>
And the proof is completed, as indicated by the message: <p>
<p>
<pre>
  Proof completed.
</pre>
<p>
 And there lies our first proof.  Already, even for simple theorems like this, the final proof script is unstructured and not very enlightening to readers.  If we extend this approach to more serious theorems, we arrive at the unreadable proof scripts that are the favorite complaints of opponents of tactic-based proving.  Fortunately, Coq has rich support for scripted automation, and we can take advantage of such a scripted tactic (defined elsewhere) to make short work of this lemma.  We abort the old proof attempt and start again.
</div>
<div><textarea id='coq-ta-37'>
Abort.
</textarea></div>
<div><p>
 </div>
<div><textarea id='coq-ta-38'>
Lemma compile_correct' : forall e s p, progDenote (compile e ++ p) s =
  progDenote p (expDenote e :: s).
  induction e; crush.
Qed.
</textarea></div>
<div><p>
We need only to state the basic inductive proof scheme and call a tactic that automates the tedious reasoning in between.  In contrast to the period tactic terminator from our last proof, the semicolon tactic separator supports structured, compositional proofs.  The tactic <tt>t1;</tt> <tt>t2</tt> has the effect of running <tt>t1</tt> and then running <tt>t2</tt> on each remaining subgoal.  The semicolon is one of the most fundamental building blocks of effective proof automation.  The period terminator is very useful for exploratory proving, where you need to see intermediate proof states, but final proofs of any serious complexity should have just one period, terminating a single compound tactic that probably uses semicolons.
<p>
The <tt>crush</tt> tactic comes from the library associated with this book and is not part of the Coq standard library.  The book's library contains a number of other tactics that are especially helpful in highly automated proofs.
<p>
The <tt>Qed</tt> command checks that the proof is finished and, if so, saves it.  The tactic commands we have written above are an example of a <em>proof script</em>, or a series of Ltac programs; while <tt>Qed</tt> uses the result of the script to generate a <em>proof term</em>, a well-typed term of Gallina.  To believe that a theorem is true, we only need to trust that the (relatively simple) checker for proof terms is correct; the use of proof scripts is immaterial.  Part I of this book will introduce the principles behind encoding all proofs as terms of Gallina.
<p>
The proof of our main theorem is now easy.  We prove it with four period-terminated tactics, though separating them with semicolons would work as well; the version here is easier to step through. </div>
<div><textarea id='coq-ta-39'>
Theorem compile_correct : forall e, progDenote (compile e) nil = Some (expDenote e :: nil).
  intros.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
  e : exp
  ============================
   progDenote (compile e) nil = Some (expDenote e :: nil)
 <p>
</pre><p>
At this point, we want to massage the lefthand side to match the statement of <tt>compile_correct'</tt>.  A theorem from the standard library is useful: </div>
<div><textarea id='coq-ta-40'>
Check app_nil_end.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
app_nil_end
     : forall (A : Type) (l : list A), l = l ++ nil<p>
</pre> </div>
<div><textarea id='coq-ta-41'>
  rewrite (app_nil_end (compile e)).
</textarea></div>
<div><p>
This time, we explicitly specify the value of the variable <tt>l</tt> from the theorem statement, since multiple expressions of list type appear in the conclusion.  The <tt>rewrite</tt> tactic might choose the wrong place to rewrite if we did not specify which we want.
<p>
<pre class="inline-coq" data-lang="coq">
  e : exp
  ============================
   progDenote (compile e ++ nil) nil = Some (expDenote e :: nil)
 <p>
</pre><p>
Now we can apply the lemma. </div>
<div><textarea id='coq-ta-42'>
  rewrite compile_correct'.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
  e : exp
  ============================
   progDenote nil (expDenote e :: nil) = Some (expDenote e :: nil)
 <p>
</pre><p>
We are almost done.  The lefthand and righthand sides can be seen to match by simple symbolic evaluation.  That means we are in luck, because Coq identifies any pair of terms as equal whenever they normalize to the same result by symbolic evaluation.  By the definition of <tt>progDenote</tt>, that is the case here, but we do not need to worry about such details.  A simple invocation of <tt>reflexivity</tt> does the normalization and checks that the two results are syntactically equal. </div>
<div><textarea id='coq-ta-43'>
  reflexivity.
Qed.
</textarea></div>
<div><p>
This proof can be shortened and automated, but we leave that task as an exercise for the reader. <p>
<h1>
 Typed Expressions
</h1>
<p>
 In this section, we will build on the initial example by adding additional expression forms that depend on static typing of terms for safety. <p>
<h2>
 Source Language
</h2>
<p>
 We define a trivial language of types to classify our expressions: </div>
<div><textarea id='coq-ta-44'>
Inductive type : Set := Nat | Bool.
</textarea></div>
<div><p>
Like most programming languages, Coq uses case-sensitive variable names, so that our user-defined type <tt>type</tt> is distinct from the <tt>Type</tt> keyword that we have already seen appear in the statement of a polymorphic theorem (and that we will meet in more detail later), and our constructor names <tt>Nat</tt> and <tt>Bool</tt> are distinct from the types <tt>nat</tt> and <tt>bool</tt> in the standard library.
<p>
   Now we define an expanded set of binary operators. </div>
<div><textarea id='coq-ta-45'>
Inductive tbinop : type -> type -> type -> Set :=
| TPlus : tbinop Nat Nat Nat
| TTimes : tbinop Nat Nat Nat
| TEq : forall t, tbinop t t Bool
| TLt : tbinop Nat Nat Bool.
</textarea></div>
<div><p>
The definition of <tt>tbinop</tt> is different from <tt>binop</tt> in an important way.  Where we declared that <tt>binop</tt> has type <tt>Set</tt>, here we declare that <tt>tbinop</tt> has type <tt>type</tt> <tt>-></tt> <tt>type</tt> <tt>-></tt> <tt>type</tt> <tt>-></tt> <tt>Set</tt>.  We define <tt>tbinop</tt> as an <em>indexed type family</em>.  Indexed inductive types are at the heart of Coq's expressive power; almost everything else of interest is defined in terms of them.
<p>
The intuitive explanation of <tt>tbinop</tt> is that a <tt>tbinop</tt> <tt>t1</tt> <tt>t2</tt> <tt>t</tt> is a binary operator whose operands should have types <tt>t1</tt> and <tt>t2</tt>, and whose result has type <tt>t</tt>.  For instance, constructor <tt>TLt</tt> (for less-than comparison of numbers) is assigned type <tt>tbinop</tt> <tt>Nat</tt> <tt>Nat</tt> <tt>Bool</tt>, meaning the operator's arguments are naturals and its result is Boolean.  The type of <tt>TEq</tt> introduces a small bit of additional complication via polymorphism: we want to allow equality comparison of any two values of any type, as long as they have the <em>same</em> type.
<p>
ML and Haskell have indexed algebraic datatypes.  For instance, their list types are indexed by the type of data that the list carries.  However, compared to Coq, ML and Haskell 98 place two important restrictions on datatype definitions.
<p>
First, the indices of the range of each data constructor must be type variables bound at the top level of the datatype definition.  There is no way to do what we did here, where we, for instance, say that <tt>TPlus</tt> is a constructor building a <tt>tbinop</tt> whose indices are all fixed at <tt>Nat</tt>.   <em>Generalized algebraic datatypes</em> (GADTs) are a popular feature in GHC Haskell, OCaml 4, and other languages that removes this first restriction.
<p>
The second restriction is not lifted by GADTs.  In ML and Haskell, indices of types must be types and may not be <em>expressions</em>.  In Coq, types may be indexed by arbitrary Gallina terms.  Type indices can live in the same universe as programs, and we can compute with them just like regular programs.  Haskell supports a hobbled form of computation in type indices based on multi-parameter type classes, and recent extensions like type functions bring Haskell programming even closer to <quote>real</quote> functional programming with types, but, without dependent typing, there must always be a gap between how one programs with types and how one programs normally.
<p>
 We can define a similar type family for typed expressions, where a term of type <tt>texp</tt> <tt>t</tt> can be assigned object language type <tt>t</tt>.  (It is conventional in the world of interactive theorem proving to call the language of the proof assistant the <em>meta language</em> and a language being formalized the <em>object language</em>.) </div>
<div><textarea id='coq-ta-46'>
Inductive texp : type -> Set :=
| TNConst : nat -> texp Nat
| TBConst : bool -> texp Bool
| TBinop : forall t1 t2 t, tbinop t1 t2 t -> texp t1 -> texp t2 -> texp t.
</textarea></div>
<div><p>
Thanks to our use of dependent types, every well-typed <tt>texp</tt> represents a well-typed source expression, by construction.  This turns out to be very convenient for many things we might want to do with expressions.  For instance, it is easy to adapt our interpreter approach to defining semantics.  We start by defining a function mapping the types of our object language into Coq types: </div>
<div><textarea id='coq-ta-47'>
Definition typeDenote (t : type) : Set :=
  match t with
    | Nat => nat
    | Bool => bool
  end.
</textarea></div>
<div><p>
It can take a few moments to come to terms with the fact that <tt>Set</tt>, the type of types of programs, is itself a first-class type, and that we can write functions that return <tt>Set</tt>s.  Past that wrinkle, the definition of <tt>typeDenote</tt> is trivial, relying on the <tt>nat</tt> and <tt>bool</tt> types from the Coq standard library.  We can interpret binary operators by relying on standard-library equality test functions <tt>eqb</tt> and <tt>beq_nat</tt> for Booleans and naturals, respectively, along with a less-than test <tt>leb</tt>: </div>
<div><textarea id='coq-ta-48'>
Definition tbinopDenote arg1 arg2 res (b : tbinop arg1 arg2 res)
  : typeDenote arg1 -> typeDenote arg2 -> typeDenote res :=
  match b with
    | TPlus => plus
    | TTimes => mult
    | TEq Nat => beq_nat
    | TEq Bool => eqb
    | TLt => leb
  end.
</textarea></div>
<div><p>
This function has just a few differences from the denotation functions we saw earlier.  First, <tt>tbinop</tt> is an indexed type, so its indices become additional arguments to <tt>tbinopDenote</tt>.  Second, we need to perform a genuine <em>dependent pattern match</em>, where the necessary <em>type</em> of each case body depends on the <em>value</em> that has been matched.  At this early stage, we will not go into detail on the many subtle aspects of Gallina that support dependent pattern-matching, but the subject is central to Part II of the book.
<p>
The same tricks suffice to define an expression denotation function in an unsurprising way.  Note that the <tt>type</tt> arguments to the <tt>TBinop</tt> constructor must be included explicitly in pattern-matching, but here we write underscores because we do not need to refer to those arguments directly. </div>
<div><textarea id='coq-ta-49'>
Fixpoint texpDenote t (e : texp t) : typeDenote t :=
  match e with
    | TNConst n => n
    | TBConst b => b
    | TBinop _ _ _ b e1 e2 => (tbinopDenote b) (texpDenote e1) (texpDenote e2)
  end.
</textarea></div>
<div><p>
We can evaluate a few example programs to convince ourselves that this semantics is correct. </div>
<div><textarea id='coq-ta-50'>
Eval simpl in texpDenote (TNConst 42).
</textarea></div>
<div><p>
<tt>=</tt> <tt>42</tt> <tt>:</tt> <tt>typeDenote</tt> <tt>Nat</tt> </div>
<div><textarea id='coq-ta-51'>
Eval simpl in texpDenote (TBConst true).
</textarea></div>
<div><p>
<tt>=</tt> <tt>true</tt> <tt>:</tt> <tt>typeDenote</tt> <tt>Bool</tt> </div>
<div><textarea id='coq-ta-52'>
Eval simpl in texpDenote (TBinop TTimes (TBinop TPlus (TNConst 2) (TNConst 2))
  (TNConst 7)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>28</tt> <tt>:</tt> <tt>typeDenote</tt> <tt>Nat</tt> </div>
<div><textarea id='coq-ta-53'>
Eval simpl in texpDenote (TBinop (TEq Nat) (TBinop TPlus (TNConst 2) (TNConst 2))
  (TNConst 7)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>false</tt> <tt>:</tt> <tt>typeDenote</tt> <tt>Bool</tt> </div>
<div><textarea id='coq-ta-54'>
Eval simpl in texpDenote (TBinop TLt (TBinop TPlus (TNConst 2) (TNConst 2))
  (TNConst 7)).
</textarea></div>
<div><p>
<tt>=</tt> <tt>true</tt> <tt>:</tt> <tt>typeDenote</tt> <tt>Bool</tt> <p>
 Now we are ready to define a suitable stack machine target for compilation. <p>
<h2>
 Target Language
</h2>
<p>
 In the example of the untyped language, stack machine programs could encounter stack underflows and <quote>get stuck.</quote>  This was unfortunate, since we had to deal with this complication even though we proved that our compiler never produced underflowing programs.  We could have used dependent types to force all stack machine programs to be underflow-free.
<p>
For our new languages, besides underflow, we also have the problem of stack slots with naturals instead of bools or vice versa.  This time, we will use indexed typed families to avoid the need to reason about potential failures.
<p>
We start by defining stack types, which classify sets of possible stacks. </div>
<div><textarea id='coq-ta-55'>
Definition tstack := list type.
</textarea></div>
<div><p>
Any stack classified by a <tt>tstack</tt> must have exactly as many elements, and each stack element must have the type found in the same position of the stack type.
<p>
We can define instructions in terms of stack types, where every instruction's type tells us what initial stack type it expects and what final stack type it will produce. </div>
<div><textarea id='coq-ta-56'>
Inductive tinstr : tstack -> tstack -> Set :=
| TiNConst : forall s, nat -> tinstr s (Nat :: s)
| TiBConst : forall s, bool -> tinstr s (Bool :: s)
| TiBinop : forall arg1 arg2 res s,
  tbinop arg1 arg2 res -> tinstr (arg1 :: arg2 :: s) (res :: s).
</textarea></div>
<div><p>
Stack machine programs must be a similar inductive family, since, if we again used the <tt>list</tt> type family, we would not be able to guarantee that intermediate stack types match within a program. </div>
<div><textarea id='coq-ta-57'>
Inductive tprog : tstack -> tstack -> Set :=
| TNil : forall s, tprog s s
| TCons : forall s1 s2 s3,
  tinstr s1 s2 -> tprog s2 s3 -> tprog s1 s3.
</textarea></div>
<div><p>
Now, to define the semantics of our new target language, we need a representation for stacks at runtime.  We will again take advantage of type information to define types of value stacks that, by construction, contain the right number and types of elements. </div>
<div><textarea id='coq-ta-58'>
Fixpoint vstack (ts : tstack) : Set :=
  match ts with
    | nil => unit
    | t :: ts' => typeDenote t * vstack ts'
  end%type.
</textarea></div>
<div><p>
This is another <tt>Set</tt>-valued function.  This time it is recursive, which is perfectly valid, since <tt>Set</tt> is not treated specially in determining which functions may be written.  We say that the value stack of an empty stack type is any value of type <tt>unit</tt>, which has just a single value, <tt>tt</tt>.  A nonempty stack type leads to a value stack that is a pair, whose first element has the proper type and whose second element follows the representation for the remainder of the stack type.  We write <tt>%</tt><tt>type</tt> as an instruction to Coq's extensible parser.  In particular, this directive applies to the whole <tt>match</tt> expression, which we ask to be parsed as though it were a type, so that the operator <tt>*</tt> is interpreted as Cartesian product instead of, say, multiplication.  (Note that this use of <tt>type</tt> has no connection to the inductive type <tt>type</tt> that we have defined.)
<p>
This idea of programming with types can take a while to internalize, but it enables a very simple definition of instruction denotation.  Our definition is like what you might expect from a Lisp-like version of ML that ignored type information.  Nonetheless, the fact that <tt>tinstrDenote</tt> passes the type-checker guarantees that our stack machine programs can never go wrong.  We use a special form of <tt>let</tt> to destructure a multi-level tuple. </div>
<div><textarea id='coq-ta-59'>
Definition tinstrDenote ts ts' (i : tinstr ts ts') : vstack ts -> vstack ts' :=
  match i with
    | TiNConst _ n => fun s => (n, s)
    | TiBConst _ b => fun s => (b, s)
    | TiBinop _ _ _ _ b => fun s =>
      let '(arg1, (arg2, s')) := s in
        ((tbinopDenote b) arg1 arg2, s')
  end.
</textarea></div>
<div><p>
Why do we choose to use an anonymous function to bind the initial stack in every case of the <tt>match</tt>?  Consider this well-intentioned but invalid alternative version:
<pre class="inline-coq" data-lang="coq">
Definition tinstrDenote ts ts' (i : tinstr ts ts') (s : vstack ts) : vstack ts' :=
  match i with
    | TiNConst _ n => (n, s)
    | TiBConst _ b => (b, s)
    | TiBinop _ _ _ _ b =>
      let '(arg1, (arg2, s')) := s in
        ((tbinopDenote b) arg1 arg2, s')
  end.<p>
</pre><p>
The Coq type checker complains that:
<p>
<pre>
The term "(n, s)" has type "(nat * vstack ts)%type"
 while it is expected to have type "vstack ?119".
</pre>
<p>
This and other mysteries of Coq dependent typing we postpone until Part II of the book.  The upshot of our later discussion is that it is often useful to push inside of <tt>match</tt> branches those function parameters whose types depend on the type of the value being matched.  Our later, more complete treatment of Gallina's typing rules will explain why this helps.
<p>
 We finish the semantics with a straightforward definition of program denotation. </div>
<div><textarea id='coq-ta-60'>
Fixpoint tprogDenote ts ts' (p : tprog ts ts') : vstack ts -> vstack ts' :=
  match p with
    | TNil _ => fun s => s
    | TCons _ _ _ i p' => fun s => tprogDenote p' (tinstrDenote i s)
  end.
</textarea></div>
<div><p>
The same argument-postponing trick is crucial for this definition. <p>
<h2>
 Translation
</h2>
<p>
 To define our compilation, it is useful to have an auxiliary function for concatenating two stack machine programs. </div>
<div><textarea id='coq-ta-61'>
Fixpoint tconcat ts ts' ts'' (p : tprog ts ts') : tprog ts' ts'' -> tprog ts ts'' :=
  match p with
    | TNil _ => fun p' => p'
    | TCons _ _ _ i p1 => fun p' => TCons i (tconcat p1 p')
  end.
</textarea></div>
<div><p>
With that function in place, the compilation is defined very similarly to how it was before, modulo the use of dependent typing. </div>
<div><textarea id='coq-ta-62'>
Fixpoint tcompile t (e : texp t) (ts : tstack) : tprog ts (t :: ts) :=
  match e with
    | TNConst n => TCons (TiNConst _ n) (TNil _)
    | TBConst b => TCons (TiBConst _ b) (TNil _)
    | TBinop _ _ _ b e1 e2 => tconcat (tcompile e2 _)
      (tconcat (tcompile e1 _) (TCons (TiBinop _ b) (TNil _)))
  end.
</textarea></div>
<div><p>
One interesting feature of the definition is the underscores appearing to the right of <tt>=></tt> arrows.  Haskell and ML programmers are quite familiar with compilers that infer type parameters to polymorphic values.  In Coq, it is possible to go even further and ask the system to infer arbitrary terms, by writing underscores in place of specific values.  You may have noticed that we have been calling functions without specifying all of their arguments.  For instance, the recursive calls here to <tt>tcompile</tt> omit the <tt>t</tt> argument.  Coq's <em>implicit argument</em> mechanism automatically inserts underscores for arguments that it will probably be able to infer.  Inference of such values is far from complete, though; generally, it only works in cases similar to those encountered with polymorphic type instantiation in Haskell and ML.
<p>
The underscores here are being filled in with stack types.  That is, the Coq type inferencer is, in a sense, inferring something about the flow of control in the translated programs.  We can take a look at exactly which values are filled in: </div>
<div><textarea id='coq-ta-63'>
Print tcompile.
</textarea></div>
<div><p>
<pre class="inline-coq" data-lang="coq">
tcompile = 
fix tcompile (t : type) (e : texp t) (ts : tstack) {struct e} :
  tprog ts (t :: ts) :=
  match e in (texp t0) return (tprog ts (t0 :: ts)) with
  | TNConst n => TCons (TiNConst ts n) (TNil (Nat :: ts))
  | TBConst b => TCons (TiBConst ts b) (TNil (Bool :: ts))
  | TBinop arg1 arg2 res b e1 e2 =>
      tconcat (tcompile arg2 e2 ts)
        (tconcat (tcompile arg1 e1 (arg2 :: ts))
           (TCons (TiBinop ts b) (TNil (res :: ts))))
  end
     : forall t : type, texp t -> forall ts : tstack, tprog ts (t :: ts)<p>
</pre><p>
 We can check that the compiler generates programs that behave appropriately on our sample programs from above: </div>
<div><textarea id='coq-ta-64'>
Eval simpl in tprogDenote (tcompile (TNConst 42) nil) tt.
</textarea></div>
<div><p>
<tt>=</tt> <tt>(42,</tt> <tt>tt)</tt> <tt>:</tt> <tt>vstack</tt> <tt>(Nat</tt> <tt>::</tt> <tt>nil)</tt> </div>
<div><textarea id='coq-ta-65'>
Eval simpl in tprogDenote (tcompile (TBConst true) nil) tt.
</textarea></div>
<div><p>
<tt>=</tt> <tt>(true,</tt> <tt>tt)</tt> <tt>:</tt> <tt>vstack</tt> <tt>(Bool</tt> <tt>::</tt> <tt>nil)</tt> </div>
<div><textarea id='coq-ta-66'>
Eval simpl in tprogDenote (tcompile (TBinop TTimes (TBinop TPlus (TNConst 2)
  (TNConst 2)) (TNConst 7)) nil) tt.
</textarea></div>
<div><p>
<tt>=</tt> <tt>(28,</tt> <tt>tt)</tt> <tt>:</tt> <tt>vstack</tt> <tt>(Nat</tt> <tt>::</tt> <tt>nil)</tt> </div>
<div><textarea id='coq-ta-67'>
Eval simpl in tprogDenote (tcompile (TBinop (TEq Nat) (TBinop TPlus (TNConst 2)
  (TNConst 2)) (TNConst 7)) nil) tt.
</textarea></div>
<div><p>
<tt>=</tt> <tt>(false,</tt> <tt>tt)</tt> <tt>:</tt> <tt>vstack</tt> <tt>(Bool</tt> <tt>::</tt> <tt>nil)</tt> </div>
<div><textarea id='coq-ta-68'>
Eval simpl in tprogDenote (tcompile (TBinop TLt (TBinop TPlus (TNConst 2) (TNConst 2))
  (TNConst 7)) nil) tt.
</textarea></div>
<div><p>
<tt>=</tt> <tt>(true,</tt> <tt>tt)</tt> <tt>:</tt> <tt>vstack</tt> <tt>(Bool</tt> <tt>::</tt> <tt>nil)</tt> <p>
 The compiler seems to be working, so let us turn to proving that it <em>always</em> works. <p>
<h2>
 Translation Correctness
</h2>
<p>
 We can state a correctness theorem similar to the last one. </div>
<div><textarea id='coq-ta-69'>
Theorem tcompile_correct : forall t (e : texp t),
  tprogDenote (tcompile e nil) tt = (texpDenote e, tt).
</textarea></div>
<div><p>
Again, we need to strengthen the theorem statement so that the induction will go through.  This time, to provide an excuse to demonstrate different tactics, I will develop an alternative approach to this kind of proof, stating the key lemma as: </div>
<div><textarea id='coq-ta-70'>
Lemma tcompile_correct' : forall t (e : texp t) ts (s : vstack ts),
  tprogDenote (tcompile e ts) s = (texpDenote e, s).
</textarea></div>
<div><p>
While lemma <tt>compile_correct'</tt> quantified over a program that is the <quote>continuation</quote> for the expression we are considering, here we avoid drawing in any extra syntactic elements.  In addition to the source expression and its type, we also quantify over an initial stack type and a stack compatible with it.  Running the compilation of the program starting from that stack, we should arrive at a stack that differs only in having the program's denotation pushed onto it.
<p>
   Let us try to prove this theorem in the same way that we settled on in the last section. </div>
<div><textarea id='coq-ta-71'>
  induction e; crush.
</textarea></div>
<div><p>
We are left with this unproved conclusion:
<p>
<pre class="inline-coq" data-lang="coq">
tprogDenote
     (tconcat (tcompile e2 ts)
        (tconcat (tcompile e1 (arg2 :: ts))
           (TCons (TiBinop ts t) (TNil (res :: ts))))) s =
   (tbinopDenote t (texpDenote e1) (texpDenote e2), s)
 <p>
</pre><p>
We need an analogue to the <tt>app_assoc_reverse</tt> theorem that we used to rewrite the goal in the last section.  We can abort this proof and prove such a lemma about <tt>tconcat</tt>.
</div>
<div><textarea id='coq-ta-72'>
Abort.

Lemma tconcat_correct : forall ts ts' ts'' (p : tprog ts ts') (p' : tprog ts' ts'')
  (s : vstack ts),
  tprogDenote (tconcat p p') s
  = tprogDenote p' (tprogDenote p s).
  induction p; crush.
Qed.
</textarea></div>
<div><p>
This one goes through completely automatically.
<p>
Some code behind the scenes registers <tt>app_assoc_reverse</tt> for use by <tt>crush</tt>.  We must register <tt>tconcat_correct</tt> similarly to get the same effect: </div>
<div><textarea id='coq-ta-73'>
Hint Rewrite tconcat_correct.
</textarea></div>
<div><p>
Here we meet the pervasive concept of a <em>hint</em>.  Many proofs can be found through exhaustive enumerations of combinations of possible proof steps; hints provide the set of steps to consider.  The tactic <tt>crush</tt> is applying such brute force search for us silently, and it will consider more possibilities as we add more hints.  This particular hint asks that the lemma be used for left-to-right rewriting.
<p>
Now we are ready to return to <tt>tcompile_correct'</tt>, proving it automatically this time. </div>
<div><textarea id='coq-ta-74'>
Lemma tcompile_correct' : forall t (e : texp t) ts (s : vstack ts),
  tprogDenote (tcompile e ts) s = (texpDenote e, s).
  induction e; crush.
Qed.
</textarea></div>
<div><p>
We can register this main lemma as another hint, allowing us to prove the final theorem trivially. </div>
<div><textarea id='coq-ta-75'>
Hint Rewrite tcompile_correct'.

Theorem tcompile_correct : forall t (e : texp t),
  tprogDenote (tcompile e nil) tt = (texpDenote e, tt).
  crush.
Qed.
</textarea></div>
<div><p>
It is probably worth emphasizing that we are doing more than building mathematical models.  Our compilers are functional programs that can be executed efficiently.  One strategy for doing so is based on <em>program extraction</em>, which generates OCaml code from Coq developments.  For instance, we run a command to output the OCaml version of <tt>tcompile</tt>: </div>
<div><textarea id='coq-ta-76'>
Extraction tcompile.
</textarea></div>
<div><p>
<pre>
let rec tcompile t e ts =
  match e with
  | TNConst n ->
    TCons (ts, (Cons (Nat, ts)), (Cons (Nat, ts)), (TiNConst (ts, n)), (TNil
      (Cons (Nat, ts))))
  | TBConst b ->
    TCons (ts, (Cons (Bool, ts)), (Cons (Bool, ts)), (TiBConst (ts, b)),
      (TNil (Cons (Bool, ts))))
  | TBinop (t1, t2, t0, b, e1, e2) ->
    tconcat ts (Cons (t2, ts)) (Cons (t0, ts)) (tcompile t2 e2 ts)
      (tconcat (Cons (t2, ts)) (Cons (t1, (Cons (t2, ts)))) (Cons (t0, ts))
        (tcompile t1 e1 (Cons (t2, ts))) (TCons ((Cons (t1, (Cons (t2,
        ts)))), (Cons (t0, ts)), (Cons (t0, ts)), (TiBinop (t1, t2, t0, ts,
        b)), (TNil (Cons (t0, ts))))))
</pre>
<p>
We can compile this code with the usual OCaml compiler and obtain an executable program with halfway decent performance.
<p>
This chapter has been a whirlwind tour through two examples of the style of Coq development that I advocate.  Parts II and III of the book focus on the key elements of that style, namely dependent types and scripted proof automation, respectively.  Before we get there, we will spend some time in Part I on more standard foundational material.  Part I may still be of interest to seasoned Coq hackers, since I follow the highly automated proof style even at that early stage. </div>
<div><textarea id='coq-ta-77'>
</textarea></div>
<script type="text/javascript">
 var coqdoc_ids = ['coq-ta-2',  'coq-ta-3',  'coq-ta-4', 
                  'coq-ta-5',  'coq-ta-6',  'coq-ta-7',  'coq-ta-8', 
                  'coq-ta-9',  'coq-ta-10',  'coq-ta-11',  'coq-ta-12', 
                  'coq-ta-13',  'coq-ta-14',  'coq-ta-15',  'coq-ta-16', 
                  'coq-ta-17',  'coq-ta-18',  'coq-ta-19',  'coq-ta-20', 
                  'coq-ta-21',  'coq-ta-22',  'coq-ta-23',  'coq-ta-24', 
                  'coq-ta-25',  'coq-ta-26',  'coq-ta-27',  'coq-ta-28', 
                  'coq-ta-29',  'coq-ta-30',  'coq-ta-31',  'coq-ta-32', 
                  'coq-ta-33',  'coq-ta-34',  'coq-ta-35',  'coq-ta-36', 
                  'coq-ta-37',  'coq-ta-38',  'coq-ta-39',  'coq-ta-40', 
                  'coq-ta-41',  'coq-ta-42',  'coq-ta-43',  'coq-ta-44', 
                  'coq-ta-45',  'coq-ta-46',  'coq-ta-47',  'coq-ta-48', 
                  'coq-ta-49',  'coq-ta-50',  'coq-ta-51',  'coq-ta-52', 
                  'coq-ta-53',  'coq-ta-54',  'coq-ta-55',  'coq-ta-56', 
                  'coq-ta-57',  'coq-ta-58',  'coq-ta-59',  'coq-ta-60', 
                  'coq-ta-61',  'coq-ta-62',  'coq-ta-63',  'coq-ta-64', 
                  'coq-ta-65',  'coq-ta-66',  'coq-ta-67',  'coq-ta-68', 
                  'coq-ta-69',  'coq-ta-70',  'coq-ta-71',  'coq-ta-72', 
                  'coq-ta-73',  'coq-ta-74',  'coq-ta-75',  'coq-ta-76', 
                  'coq-ta-77'];
</script>
  </div> <!-- /#document -->
  </div> <!-- /#code-wrapper -->
  </div> <!-- /#ide-wrapper -->

  <script src="../js/jscoq-loader.js" type="text/javascript"></script>

  <script type="text/javascript">
   var coq;

   loadJsCoq('../')
       .then(loadJs("../external/CodeMirror/addon/runmode/runmode"))
       .then(loadJs("../external/CodeMirror/addon/runmode/colorize"))
       .then( function () {
           var coqInline = document.getElementsByClassName("inline-coq");
           CodeMirror.colorize(coqInline);
           return; })
       .then( function () {
           coq = new CoqManager (coqdoc_ids,
                                 { base_path: '../', init_pkgs: ['init', 'cpdt'] }
           );
       });
  </script>

</body>
</html>

